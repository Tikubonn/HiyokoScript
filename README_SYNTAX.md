
# ひよこすくりぷとの文法説明書

こちらのページではひよこすくりぷとの文法を紹介しています。

* [コメント文](#コメント文)
* [話者を指定する](#話者を指定する)
* [複数の話者を指定する](#複数の話者を指定する)
* [立ち絵を指定する](#立ち絵を指定する)
  * [複数の立ち絵を指定する](#複数の立ち絵を指定する)
  * [立ち絵を非表示にする](#立ち絵を非表示にする)
* [変数を指定する](#変数を指定する)
  * [背景色](#背景色)
  * [背景画像](#背景画像)
  * [背景動画](#背景動画)
  * [BGM](#BGM)
  * [SE](#SE)
  * [字幕](#字幕)
  * [特殊効果:もにゅもにゅ](#特殊効果もにゅもにゅ)
  * [特殊効果:自動もにゅもにゅ](#特殊効果自動もにゅもにゅ)
  * [特殊効果:ぴょんぴょん](#特殊効果ぴょんぴょん)
  * [特殊効果:自動ぴょんぴょん](#特殊効果自動ぴょんぴょん)
  * [特殊効果:ぐにゃぐにゃ](#特殊効果ぐにゃぐにゃ)

## コメント文

```txt
--これはコメント文です
--コメントアウトされた文章は読み上げられません
```

行頭に`--`があればひよこすくりぷとはその行を無視します。

## 話者を指定する

https://user-images.githubusercontent.com/7812951/173232227-66d41556-1d95-48de-9987-6d6cb8058037.mp4

行頭に`#声名`があれば、自動的に話者がその人物に切り替わります。
また、複数の声名を`*`または`&`で区切ることで、複数の立ち絵を表示させることができます。
ただし、それぞれに別々の文章を読み上げさせることはできません。

```txt
#ずん「僕の名前はずんだもんなのだ」
#ずん 僕の名前はずんだもんなのだ。
#ずん*つむ ずんだもんとつむぎちゃんだよ
```

### 複数の話者を指定する

https://user-images.githubusercontent.com/7812951/173232232-8c522708-0c34-4aa4-9294-e7f765d5b2c3.mp4

話者の指定は、再設定されるまでずっと継続されます。
そのため、ずっと同じ話者に喋らせる場面では、このように書くこともできます。

```txt
#ずん
ここから話す内容はすべてずんだもんが喋るのだ
ずんだもんの話は校長先生よりも長いから覚悟するのだ
わかったら返事するのだ

#つむ はーい
ずんだもん張り切ってんなぁ…
```

## 立ち絵を指定する

https://user-images.githubusercontent.com/7812951/173232237-d66cdf5d-89e2-439f-8542-9fe8a075af07.mp4

行頭に`@人物名`があれば、自動的にその人物の立ち絵が表示されます。
また、複数の人物名を`*`または`&`で区切ることで、複数の立ち絵を表示させることができます。

当然ですが、その人物に立ち絵が設定されていなければ何も表示されません。
また、その人物にデフォルト立ち絵が設定されていない場合、会話中以外では立ち絵が表示されなくなります。ご注意ください。

```txt
$背景色=ffffff
@ずんだもん

#ずん ずんだもんが表示されているのだ

@ずんだもん*つむぎちゃん

#ずんにこ ずんだもんとつむぎちゃんが表示されているのだ
```

### 複数の立ち絵を指定する

https://user-images.githubusercontent.com/7812951/173232241-caea22b9-a3fc-4185-a88e-16ba4f1a9d9b.mp4

話者の指定と同様に、立ち絵の指定も再設定されるまでずっと継続されます。
そのため、ずっと同じ立ち絵を表示させる場面では、このように書くこともできます。

```txt
$背景色=ffffff
@ずんだもん

#ずん ずんだもんが表示されているのだ
#つむ そうだね

@ずんだもん*つむぎちゃん

#ずん ずんだもんとつむぎちゃんが表示されているのだ
#つむ そうだね
```

### 立ち絵を非表示にする

https://user-images.githubusercontent.com/7812951/173232250-f3b0e535-6f8b-41c0-ab0b-924dae17212c.mp4

また、人物名を空にすることで、すべての立ち絵を非表示にすることができます。

```txt
$背景色=ffffff
@ずんだもん

#ずん ずんだもんが表示されているのだ

$背景色
@

#ずん だ、だれもいないのだ…
```

## 変数を指定する

行頭が`$変数名=値`であれば、自動的に変数に値を代入します。
いくつかの変数を変更することによって、その場面に新たな画面効果を付与したり、音声を追加したりすることができます。
これらはひよこすくりぷとが標準で用意している変数とその概要になります。

```txt
$BGM=音楽ファイル.mp3
$背景画像=背景画像.jpg
#ずん 音楽、そして景色が見えるのだ

$BGM
$背景画像
#ずん 何も見えない、何も聞こえないのだ…
```

### 便利な変数

* [背景色](#背景色)
* [背景画像](#背景画像)
* [背景動画](#背景動画)
* [BGM](#BGM)
* [SE](#SE)
* [字幕](#字幕)
* [特殊効果:もにゅもにゅ](#特殊効果もにゅもにゅ)
* [特殊効果:自動もにゅもにゅ](#特殊効果自動もにゅもにゅ)
* [特殊効果:ぴょんぴょん](#特殊効果ぴょんぴょん)
* [特殊効果:自動ぴょんぴょん](#特殊効果自動ぴょんぴょん)
* [特殊効果:ぐにゃぐにゃ](#特殊効果ぐにゃぐにゃ)

### 背景色

https://user-images.githubusercontent.com/7812951/173232258-211839ac-f3dc-43ec-bb1f-9e90904c83c8.mp4

16進数のカラーコードから背景色を変更します。
未指定にするとなにも表示しません。

```txt
@ずんだもん

$背景色=ff0000
#ずん 真っ赤なのだ

$背景色=00ff00
#ずん 真緑なのだ

$背景色=0000ff
#ずん 真っ青なのだ

$背景色
#ずん なにもないのだ
```

### 背景画像

https://user-images.githubusercontent.com/7812951/173232267-f9402f33-2b5f-4b2c-91d2-3a22aa2b31b3.mp4

画像ファイルを背景画像に設定します。
画像ファイルのパスがファイル名だけであっても、設定ファイルの検索パスが適切に設定されていれば、ひよこすくりぷとは自動的にファイルを検索し、パス名を自動取得してくれます。
設定された背景画像は再設定されるまでの間ずっと継続されます。
未指定にするとなにも表示しません。

```txt
$背景色=ffffff
@ずんだもん

$背景画像=花.jpg
--背景画像全表示するか:省略可(0 or 1)
$背景画像全表示=1
#ずん 可愛らしいお花なのだ

$背景画像=秋葉原.jpg
#ずん 秋葉原なのだ

$背景画像=ヨドバシアキバ.jpg
#ずん ヨドバシアキバなのだ

$背景画像
#ずん なにもないのだ
```

### 背景動画

https://user-images.githubusercontent.com/7812951/173232272-bbb00bf5-7ed0-48e9-a983-f024e24b23a8.mp4

動画ファイルを背景画像に設定します。
動画ファイルのパスがファイル名だけであっても、設定ファイルの検索パスが適切に設定されていれば、ひよこすくりぷとは自動的にファイルを検索し、パス名を自動取得してくれます。
設定された背景画像は再設定されるまでの間ずっと継続されます。
未指定にするとなにも表示しません。

```txt
$背景色=ffffff
@ずんだもん

$背景動画=渦巻.mp4
--全画面表示するか:省略可(0 or 1)
$背景動画全表示=1
--ループするか:省略可(0 or 1)
$背景動画ループ=1
#ずん 渦巻なのだ

$背景動画=モザイク.mp4
#ずん モザイクなのだ

$背景動画=雪.mp4
#ずん 雪が降っているのだ

$背景動画
#ずん なにもないのだ
```

### BGM

https://user-images.githubusercontent.com/7812951/173232281-e4c7e832-ef2e-4ac1-99e9-ee9f73e5e0e1.mp4

音声ファイルをBGMに設定します。
音声ファイルのパスがファイル名だけであっても、設定ファイルの検索パスが適切に設定されていれば、ひよこすくりぷとは自動的にファイルを検索し、パス名を自動取得してくれます。
設定されたBGMは再設定されるまでの間ずっと継続されます。

```txt
$背景色=ffffff
@ずんだもん

$BGM=海.wav
--音量:省略可(0.0 ~)
$BGM音量=0.3
--左右:省略可(-1.0 ~ 1.0)
$BGM左右=0.0
--速度:省略可(0.0 ~)
$BGM速度=1.0
--ループするか:省略可(0 or 1)
$BGMループ=0

#ずん ずんだもんはいま海にいるのだ

$BGM=戦闘機.wav
#ずんしょぼ ずんだもんはいま紛争地帯にいるのだ
戦闘機が頭上を通過しているのだ
だ、だれか、助けてほしいのだー！
```

### SE

https://user-images.githubusercontent.com/7812951/173232289-5543f2ba-569b-4c5b-8b45-93fa2fa3a133.mp4

音声ファイルを効果音に設定します。
音声ファイルのパスがファイル名だけであっても、設定ファイルの検索パスが適切に設定されていれば、ひよこすくりぷとは自動的にファイルを検索し、パス名を自動取得してくれます。
設定された効果音は一度再生されると初期化されます。

```txt
$背景色=ffffff
$自動もにゅもにゅ=1
@ずんだもん

$SE=もちっ.mp3
--音量:省略可(0.0 ~)
$SE音量=0.3 
--左右:省略可(-1.0 ~ 1.0)
$SE左右=0.0 
--速度:省略可(0.0 ~)
$SE速度=1.0 
#ずん もちっ！

$SE=もちっ2.mp3
#ずん もちっ！

$SE=もちっ3.mp3
#ずん もちっ！
```

### 字幕

https://user-images.githubusercontent.com/7812951/173232297-7e904b5c-36d4-4187-acde-813974db6ea2.mp4

ひよこすくりぷとは字幕の色・サイズ・フォントを変数から変更することができます。
これらの変更は再設定されるまで継続されます。

```txt
--フォント:省略可
$字幕フォント=Meiryo 
--サイズ:省略可(0 ~)
$字幕サイズ=50 
--色:省略可(000000 ~ ffffff)
$字幕色=ff0000 
$背景色=ffffff
@ずんだもん

#ずんおこ ずんだもんは怒っているのだ

$字幕フォント
$字幕サイズ
$字幕色

#ずん 普通のずんだもんなのだ
```

### 特殊効果:もにゅもにゅ

https://user-images.githubusercontent.com/7812951/173232313-e34c2a15-7cd2-427d-8892-60285345411b.mp4

試験的に搭載されたエフェクトです。
指定された人物をもにゅもにゅ動かします。
話者指定と同様に`*`あるいは`&`記号を用い、ターゲットを複数指定することもできます。

```txt
$背景色=ffffff
@ずんだもん*つむぎちゃん

$もにゅもにゅ=ずんだもん
--変形比:省略可(0.0 ~ 2.0)
$もにゅもにゅ変形比=0.95
--周期:省略可(0.0 ~)
$もにゅもにゅ周期=0.5

#ずん もにゅもにゅしてるのだ！

$もにゅもにゅ=ずんだもん*つむぎちゃん
#つむ もにゅもにゅしてるね！
```

### 特殊効果:自動もにゅもにゅ

https://user-images.githubusercontent.com/7812951/173232321-af07cc78-8d08-492e-bec0-491fa075ed09.mp4

試験的に搭載されたエフェクトです。
こちらは会話中の人物をもにゅもにゅさせます。
このエフェクトは`$もにゅもにゅ`と重複するため、同時使用するときには注意が必要です。

```txt
$背景色=ffffff
@ずんだもん*つむぎちゃん

$自動もにゅもにゅ=1
#ずん 会話中もにゅもにゅするのだ
#つむ 会話中もにゅもにゅするね

$自動もにゅもにゅ=0
#ずん 会話中もにゅもにゅしないのだ
#つむ 会話中もにゅもにゅしないね
```

### 特殊効果:ぴょんぴょん

https://user-images.githubusercontent.com/7812951/173232328-0d3c72cf-ae6a-4d29-93f2-0828694ce100.mp4

試験的に搭載されたエフェクトです。
指定された人物をぴょんぴょん跳ねさせます。
話者指定と同様に`*`あるいは`&`記号を用い、ターゲットを複数指定することもできます。

```txt
$背景色=ffffff
@ずんだもん*つむぎちゃん

$ぴょんぴょん=ずんだもん
--変形比:省略可(0.0 ~ 2.0)
$ぴょんぴょん変形比=0.95
--高さ:省略可(0.0 ~)
$ぴょんぴょん高さ=50
--周期:省略可(0.0 ~)
$ぴょんぴょん周期=0.5
#ずん すっごく跳ねるのだ！

$ぴょんぴょん=ずんだもん*つむぎちゃん
#つむ すっごく跳ねるね！
```

### 特殊効果:自動ぴょんぴょん

https://user-images.githubusercontent.com/7812951/173232333-ec25bc65-da80-4329-90f0-c952cb4577da.mp4

試験的に搭載されたエフェクトです。
こちらは会話中の人物をぴょんぴょん跳ねさせます。
このエフェクトは`$ぴょんぴょん`と重複するため、同時使用するときには注意が必要です。

```txt
$背景色=ffffff
@ずんだもん*つむぎちゃん

$自動ぴょんぴょん=1
#ずん 会話中ぴょんぴょんするのだ
#つむ 会話中ぴょんぴょんするね

$自動ぴょんぴょん=0
#ずん 会話中ぴょんぴょんしないのだ
#つむ 会話中ぴょんぴょんしないね
```

### 特殊効果:ぐにゃぐにゃ

https://user-images.githubusercontent.com/7812951/173232338-f668a51a-f2ff-4e78-a86f-468d92669ddc.mp4

試験的に搭載されたエフェクトです。
指定された人物をぐにゃぐにゃ揺らします。
話者指定と同様に`*`あるいは`&`記号を用い、ターゲットを複数指定することもできます。

```txt
$背景色=ffffff
@ずんだもん*つむぎちゃん

$ぐにゃぐにゃ=ずんだもん
--横幅:省略可(0.0 ~)
$ぐにゃぐにゃ横幅=100
--縦幅:省略可(0.0 ~)
$ぐにゃぐにゃ縦幅=100
--周期:省略可(0.0 ~)
$ぐにゃぐにゃ周期=1.0
#ずん うわー、た、たすけてー！

$ぐにゃぐにゃ=ずんだもん*つむぎちゃん
#つむ わ、わたしまでー！
```
